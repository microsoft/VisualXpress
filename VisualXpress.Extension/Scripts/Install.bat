@ECHO OFF
SETLOCAL ENABLEDELAYEDEXPANSION

SET SOURCE_INSTALL_FOLDER=%1
IF "%SOURCE_INSTALL_FOLDER%"=="" (
	SET SOURCE_INSTALL_FOLDER=%~dp0
	SET SOURCE_INSTALL_FOLDER=!SOURCE_INSTALL_FOLDER:~0,-1!
)

SET VSWHERE_EXE=%ProgramFiles(x86)%\Microsoft Visual Studio\Installer\vswhere.exe
SET VSIXINSTALLER_EXE=
FOR /F "tokens=*" %%A IN ('"%VSWHERE_EXE%" -sort -property InstallationPath') DO (
	SET VSIXINSTALLER_EXE=%%A\Common7\IDE\VSIXInstaller.exe
	IF EXIST "!VSIXINSTALLER_EXE!" GOTO :FOUND_VSIXINSTALLER
)
ECHO Failed to find VSIXInstaller.exe
PAUSE
GOTO :EOF

:FOUND_VSIXINSTALLER
FOR %%V IN (2017 2019 2022) DO (
	SET SOURCE_PACKAGE_FILE=%SOURCE_INSTALL_FOLDER%\%%V\VisualXpress.%%V.vsix
	IF EXIST "!SOURCE_PACKAGE_FILE!" (
		ECHO Installing: !SOURCE_PACKAGE_FILE!
		"!VSIXINSTALLER_EXE!" /q "!SOURCE_PACKAGE_FILE!"
	) ELSE (
		ECHO Skipping: !SOURCE_PACKAGE_FILE!
	)
)


